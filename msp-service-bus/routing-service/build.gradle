apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'jacoco'

version = '1.0.0.SNAPSHOT'

def springVersion = '4.1.6.RELEASE'
def springSecurityVersion = '4.0.1.RELEASE'
def springIntVersion = '4.1.4.RELEASE'
def slf4jlog4jVersion = '1.7.12'
def hamcrestVersion = '1.3'
def httpClientVersion = '4.4.1'
def tomcatHome = 'F:/apache-tomcat-7.0.47'
def tomcatDeployment = tomcatHome+'/webapps'

httpPort = 18080
stopPort = 19090
stopKey = "stop"

repositories{
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

dependencies{
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"
    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.integration:spring-integration-core:$springIntVersion"
    compile "org.springframework.integration:spring-integration-http:$springIntVersion"
    compile "org.springframework.integration:spring-integration-xml:$springIntVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jlog4jVersion"
    testCompile "org.springframework.integration:spring-integration-test:$springIntVersion"
    testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
    runtime "org.apache.httpcomponents:httpclient:$httpClientVersion"
}

war{
    archiveName = 'routing-service.war'
}

jettyRunWar{
    war{
        from('src/integTest/resources') {
            include 'integ-test-config.properties'
            into 'WEB-INF/classes'
        }
        rootSpec.exclude("**/config.properties")
    }
}

jacoco {
    toolVersion = "0.7.4.201502262128"
}

test{
    exclude '**/*IntegTest.class'
    reports.html.destination = file ("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
}

task undeploy(type: Delete) {
   delete tomcatDeployment+'/routing-service.war'
 }

task deploy(dependsOn: ':routing-service:build', type: Copy) {
    from "$buildDir/libs"
    into tomcatDeployment
}

task integrationTest(type: Test){
    include '**/*IntegTest.class'
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
check.dependsOn integrationTest